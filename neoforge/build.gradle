architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        client {
            property "mixin.debug.export", "true"
            property "mixin.hotSwap", "true"
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

processResources {
    inputs.property "version", rootProject.mod_version

    filesMatching("META-INF/mods.toml") {
        expand "version": rootProject.mod_version
    }
}

String fileName = "${archives_base_name}-mc${supported_version}"

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
    excludes = ["**/debug/"]
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "neoforge"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
    excludes = ["**/debug/"]
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

//TODO https://github.com/SpongePowered/Mixin/issues/408
/*afterEvaluate {
    loom {
        runs {
            configureEach {
                def mixinJarFile = configurations.compileClasspath.files { it.name == "mixin-patched" }.first()
                vmArg("-javaagent:$mixinJarFile")
            }
        }
    }
}*/

def uploadDebug = true

curseforge {
    apiKey = project.hasProperty("curseforge_api_token") ? project.curseforge_api_token : ""
    project {
        id = "907390"
        changelog = rootProject.file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion "NeoForge"
        addGameVersion "Java 17"
        addGameVersion "1.20.4"

        mainArtifact(remapJar.archiveFile) {
            it.displayName = "[NeoForge ${supported_version}] FishNoStuck v${mod_version}"
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        debug uploadDebug
        forgeGradleIntegration = false
    }
}

modrinth {
    debugMode = uploadDebug
    token = project.hasProperty("modrinth_api_token") ? project.modrinth_api_token : ""
    projectId = "fish-no-stuck"
    changelog = rootProject.file("CHANGE_LOG.md").text
    syncBodyFrom = rootProject.file("README.md").text
    versionName = "[NeoForge ${supported_version}] FishNoStuck v${mod_version}"
    versionNumber = project.mod_version + "-neoforge"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.20.4"]
    loaders = ["neoforge"]
}